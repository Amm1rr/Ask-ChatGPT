B4A=true
Group=Default Group
ModulesStructureVersion=1
Type=Class
Version=12.2
@EndOfDesignText@
'This class is based on:
'https://www.b4x.com/android/forum/threads/gpt-3.145654/#content
'by Abdull Cadre
'
'All I have done is extract Abdull's code essential component (subroutine
'[generate_gpt3_response] in [ChatUI] activity) turned it into a class, tidied
'it up a bit, anglicized it and documented extensively
'
'All basically for my own education - many thanks to Abdull for doing all
'original research
Sub Class_Globals
	Private API_KEY 			As String
	Public TimeoutText 			As String = "Timeout" & CRLF & "Server is busy. Just try again." & CRLF & "سرور شلوغ است، مجددا امتحان کنید."
	Public OpenApiHostError 	As String = "api.openai.com is unreachable." & CRLF & "دسترسی به سرور وجود ندارد"
End Sub

'Initializes the object. You can add parameters to this method if needed.
Public Sub Initialize
	
	
	Dim secure As SecureMyText
		secure.Initialize("", "datacode")
'	Dim enc As String = secure.EncryptToFinalTransferText("Bearer sk-AAAAAAAAAAAAAAAAAAAAAAAAA") 't.encrypt("Bearer sk-AAAAAAAAAAAAAAAAAAAAAAAA")
	Dim enc As String = "znBwhu5Qbc1ilhM1t00vWHMkw1Or8GnLVa1HcEdo5nOMTXWD0gfnptHyfx+mclYeB1U5kVYNXnKo" & CRLF & _
						"6yzr6luiTA=="  & CRLF & _
						"Y3mDBhBbd0I="  & CRLF & _
						"PIoruDtshkcBM0Vj4KQQMA=="
	Dim dec As String = secure.decrypt(enc)
'	LogColor("Initialize:" & dec, Colors.Red)
	API_KEY = dec
End Sub

'System String 	   : The System Message helps set the behavior of the assistant.
'		     example, the assistant should instructed with "You are a helpful assistant."
'User Messages 	   : User Question
'Assistant Messages : The Assistant Messages help store prior responses. They can also be written by a developer
' 					  To help give examples of desired behavior.
Public Sub Query(system_string As String, query_string As String, assistant_string As String) As ResumableSub
    Try
 
        'Following parameter explanations I have primarily taken from
        'https://accessibleai.dev/post/generating_text_with_gpt_and_python/
        'with a few additions from various googlings
		
        'More googling and I found the definitive source:
        'https://platform.openai.com/docs/api-reference/introduction
        'there are a lot more options than used in this class
 		
        '"n": 1
        'number of completions to generate
 		
        '"stop": "None" OR "."
        'an optional setting to control response generation
		
		'"model": "text-davinci-003" OR "gpt-3.5-turbo"
        'model to be used
        'see https://beta.openai.com/docs/models/gpt-3
 		
        '"max_tokens": 350
        'maximum tokens in prompt AND response
 		
        '"temperature": 0.5
        'level of creativity in response
        'a higher value means model will take more risks, try 0.9 for more
        'creative applications, and 0 for ones with a well-defined answer
		
		'role:system message 	 - The system message helps set the behavior of the assistant. example, the assistant should instructed with "You are a helpful assistant."
		'role:user messages 	 - The user messages help instruct the assistant. They can be generated by the end users of an Application, Or set by a developer As an instruction.
		'role:assistant messages - The assistant messages help store prior responses. They can also be written by a developer To help give examples of desired behavior.
		
'	/// <summary>
'	/// Ask the API To complete the prompt(s) using the specified parameters.  This Is non-streaming, so it will wait Until the API returns the full result.  Any non-specified parameters will fall back To default values specified in <see cref="DefaultCompletionRequestArgs"/> If present.
'	/// </summary>
'	/// <param name="prompt">The prompt To generate from</param>
'	/// <param name="model">The model To use. You can use <see cref="ModelsEndpoint.GetModelsAsync()"/> To see all of your available models, Or use a standard model like <see cref="Model.DavinciText"/>.</param>
'	/// <param name="max_tokens">How many tokens To complete To. Can Return fewer If a stop sequence Is hit.</param>
'	/// <param name="temperature">What sampling temperature To use. Higher values means the model will take more risks. Try 0.9 For more creative applications, And 0 (argmax sampling) For ones with a well-defined answer. It Is generally recommend To use this Or <paramref name="top_p"/> but Not both.</param>
'	/// <param name="top_p">An alternative To sampling with temperature, called nucleus sampling, where the model considers the results of the tokens with top_p probability mass. So 0.1 means only the tokens comprising the top 10% probability mass are considered. It Is generally recommend To use this Or <paramref name="temperature"/> but Not both.</param>
'	/// <param name="numOutputs">How many different choices To request For Each prompt.</param>
'	/// <param name="presencePenalty">The scale of the penalty applied If a token Is already present at all.  Should generally be between 0 And 1, although negative numbers are allowed To encourage token reuse.</param>
'	/// <param name="frequencyPenalty">The scale of the penalty For how often a token Is used.  Should generally be between 0 And 1, although negative numbers are allowed To encourage token reuse.</param>
'	/// <param name="logProbs">Include the Log probabilities on the logprobs most likely tokens, which can be found in <see cref="CompletionResult.Completions"/> -> <see cref="Choice.Logprobs"/>. So For example, If logprobs Is 10, the API will Return a list of the 10 most likely tokens. If logprobs Is supplied, the API will always Return the logprob of the sampled token, so there may be up To logprobs+1 elements in the response.</param>
'	/// <param name="echo">Echo back the prompt in addition To the completion.</param>
'	/// <param name="stopSequences">One Or more sequences where the API will stop generating further tokens. The returned text will Not contain the stop sequence.</param>
		
		' Create a JSON object
		Dim json As Map
			json.Initialize
			json.Put("model", "gpt-3.5-turbo")
			json.Put("n", 1)
			json.Put("stop", "stop")
'			json.Put("prompt", query_string)
			json.Put("max_tokens", 2048)
			json.Put("temperature", 0.5)
			json.Put("stream", False)

		' Create an array of messages
		Dim messages As List
			messages.Initialize
		Dim systemMessage As Map
			systemMessage.Initialize
			systemMessage.Put("role", "system")
			systemMessage.Put("content", system_string)
		messages.Add(systemMessage)
		Dim userMessage As Map
			userMessage.Initialize
			userMessage.Put("role", "user")
			userMessage.Put("content", query_string)
		messages.Add(userMessage)
		Dim assistantMessage As Map
			assistantMessage.Initialize
			assistantMessage.Put("role", "assistant")
			assistantMessage.Put("content", assistant_string)
		messages.Add(assistantMessage)
		json.Put("messages", messages)
		
		Dim js As JSONGenerator
			js.Initialize(json)
		
		'Raw JSON String Generated
'		LogColor("Param: " & js.ToString, Colors.Magenta)
 		
		Dim response As String
 		
		Dim req As HttpJob
	        req.Initialize("", Me)
'		req.PostString("https://api.openai.com/v1/completions", js.ToString)
		req.PostString("https://api.openai.com/v1/chat/completions", js.ToString)
 
        'Abdull has supplied his own account API key which is very generous of
        'him but you should not use it
        'req.GetRequest.SetHeader("Authorization","Bearer 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")
 
        'You can quite easily generate your own account API key by following
        'https://accessibleai.dev/post/generating_text_with_gpt_and_python/
        'under heading [Getting a GPT-3 API Key]
		req.GetRequest.SetHeader("Authorization", API_KEY)
 
        'If you generate your own account API key then Abdull's organisation
        'key will be of no use to you
        'req.GetRequest.SetHeader("OpenAI-Organization", "org-TV3YOqDRg5DXvAUcL7dC6lI9")
 
        'If your account default organisation is "Personal" then you can supply
        'a blank organisation key - or just comment this line out
        req.GetRequest.SetHeader("OpenAI-Organization", "")
        req.GetRequest.SetContentType("application/json")
		req.GetRequest.SetContentEncoding("UTF8")
		req.GetRequest.Timeout = 30000 '30 Sec
		
        Wait For (req) JobDone(req As HttpJob)
		
        If req.Success Then
			
            'Raw JSON Response
'			LogColor("Respose: " & req.GetString, Colors.Blue)
			
			Dim parser As JSONParser
				parser.Initialize(req.GetString)
				
			Dim text 		As String  	= ParseJson(req.GetString, False)
			Dim endofconv 	As String 	= ParseJson(req.GetString, True)
			If (response <> "") Then response = response & CRLF
			response = response & text.Trim
			
			If (endofconv <> "stop") Then response = response & CRLF & "»»"
			
        Else
			If (req.ErrorMessage = "java.net.SocketTimeoutException: timeout") Then
				response = TimeoutText
			Else If (req.ErrorMessage = "java.net.UnknownHostException Unable To resolve host ""api.openai.com"": No address associated with hostname") Then
				response = OpenApiHostError
			Else
				response = "ChatGPT:Query-> ERROR Unsuccess: " & req.ErrorMessage
			End If
			
        End If
		
        req.Release
		
    Catch
		
		response = "ChatGPT:Query-> ERROR: " & LastException
		
	End Try
	
	Return response
	
End Sub

'I did as JohnC suggested in:
'https://www.b4x.com/android/forum/threads/lost-in-chatgpt-json.146738/post-930211
'and asked ChatGPT:
'using b4a how do I parse this json string: "{""id"":""chatcmpl-6t2JQdgU1ypn0ayhONAkE6bAEoGkz"",""object"":""chat.completion"",""created"":1678574948,""model"":""gpt-3.5-turbo-0301"",""usage"":{""prompt_tokens"":25,""completion_tokens"":110,""total_tokens"":135},""choices"":[{""message"":{""role"":""assistant"",""content"":""Ahoy matey, ye be askin' a great question. The worst investment be ones that promise quick riches without flappin' yer sails too much, like the \""get rich quick\"" schemes, ponzi schemes Or pyramid schemes. These scams be all about misuse of trust And deceivin' the inexperienced. They be luring investors with high promised returns, but in the end, they just take yer doubloons and disappear into the horizon. Stay away from such crooks and keep yer treasure safe, me hearty!""},""finish_reason"":""stop"",""index"":0}]}" for content
'and it responded with this - except it used a variable named "object" which
'B4A objected to that I had to change to "object_string"
'I also had to change the management of the variable "content" so the subroutine would return a result
Private Sub ParseJson(json As String, CheckEndOfConv As Boolean) As String
	Dim parser As JSONParser
		parser.Initialize(json)
	Dim root As Map
		root = parser.NextObject
	Dim id As String
		id = root.Get("id")
	Dim object_string As String
		object_string = root.Get("object")
	Dim created As Long
		created = root.Get("created")
	Dim model As String
		model = root.Get("model")
	Dim usage As Map
		usage = root.Get("usage")
	Dim promptTokens As Int
		promptTokens = usage.Get("prompt_tokens")
	Dim completionTokens As Int
		completionTokens = usage.Get("completion_tokens")
	Dim totalTokens As Int
		totalTokens = usage.Get("total_tokens")
	Dim choices As List
	choices = root.Get("choices")
	Dim choiceIndex As Int
	Dim content As String
	For choiceIndex = 0 To choices.Size - 1
		Dim choice As Map
			choice = choices.Get(choiceIndex)
		Dim message As Map
			message = choice.Get("message")
		Dim role As String
			role = message.Get("role")
		If content <> "" Then content = content & CRLF
			content = content & message.Get("content")
		Dim finishReason As String
			finishReason = choice.Get("finish_reason")
		If (CheckEndOfConv) Then _
			content = finishReason
		Log("Choice " & choiceIndex)
		Log("Role: " & role)
		Log("Content: " & content)
		Log("Finish Reason: " & finishReason)
	Next
	Return content
End Sub
